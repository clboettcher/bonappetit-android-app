plugins {
    id 'net.researchgate.release' version '2.4.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.github.clboettcher.bonappetit.app"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode project.property('versionCode') as Integer
        versionName version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

ext {
    daggerVersion = '2.4'
    retrofitVersion = '2.1.0'
    ormliteVersion = '5.0'
    jacksonVersion = "2.7.3"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android
    compile 'com.android.support:support-v13:23.2.1'

    // Bus
    compile 'org.greenrobot:eventbus:3.0.0'

    // DI
    compile "com.google.dagger:dagger:${daggerVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    provided 'javax.annotation:jsr250-api:1.0'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'javax.inject:javax.inject:1'

    // Web
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile group: 'com.squareup.retrofit2', name: 'converter-jackson', version: retrofitVersion

    // DB
    // https://mvnrepository.com/artifact/com.j256.ormlite/ormlite-core
    compile group: 'com.j256.ormlite', name: 'ormlite-core', version: ormliteVersion
    // https://mvnrepository.com/artifact/com.j256.ormlite/ormlite-android
    compile group: 'com.j256.ormlite', name: 'ormlite-android', version: ormliteVersion

    // Object Mapping
    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:${jacksonVersion}"

    // Utils
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    // https://mvnrepository.com/artifact/com.google.guava/guava
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    // Testing
    testCompile "junit:junit:4.12"
    testApt "com.google.dagger:dagger-compiler:${daggerVersion}"
}

task incrementVersionCode {
    description = "Increment the android versionCode property"
    group = "release"
    doFirst {
        def currentCode = project.property('versionCode') as Integer
        def nextCode = currentCode + 1
        println "Incrementing version code from ${currentCode} to ${nextCode}. Project version is ${version}."
        def newPropertiesFile = file('gradle.properties').text.replaceAll(
                /versionCode=\d+/,
                "versionCode=${nextCode}"
        )
        file('gradle.properties').write(newPropertiesFile)
    }
}

// Increment version code before committing the release version
confirmReleaseVersion.dependsOn incrementVersionCode
